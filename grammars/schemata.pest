WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }

file = { namespace_declaration ~ (schema | enum_rule | COMMENT)* ~ EOI }

namespace_declaration = { "namespace" ~ identifier }

schema = { "schema" ~ identifier ~ annotation* ~ "{" ~ field* ~ "}" }
field = { identifier ~ type ~ annotation* ~ (inline_schema)? ~ COMMENT? }
inline_schema = { "schema" ~ "{" ~ field* ~ "}" }
type = @{ (basic_type | complex_type) ~ ("?")? }
basic_type = { "int" | "string" | "float" | "datetime" | "bool" }
complex_type = { identifier ~ ("[]")? }

string_literal = ${ "\"" ~ inner_string ~ "\"" }
inner_string = @{ (!"\"" ~ ANY)* }
number_literal = @{ "-"? ~ ASCII_DIGIT+ }
boolean_literal = { "true" | "false" }

annotation = { "@" ~ identifier ~ ("(" ~ annotation_args ~ ")")? }
annotation_args = {
    (identifier ~ "=" ~ (string_literal | number_literal | boolean_literal)) ~
    ("," ~ identifier ~ "=" ~ (string_literal | number_literal | boolean_literal))*
}

enum_rule = { "enum" ~ identifier ~ annotation* ~ "{" ~ identifier ~ ("," ~ identifier)* ~ "}" }

identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
